import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '1.9.23'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23'
    id 'org.jlleitschuh.gradle.ktlint' version '12.1.0'
    id 'org.springdoc.openapi-gradle-plugin' version '1.8.0'
}

group = 'org.loculus'
version = '0.0.1'

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

kotlin {
    jvmToolchain(21)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "io.github.microutils:kotlin-logging-jvm:3.0.5"
    implementation "org.postgresql:postgresql:42.7.3"
    implementation "org.apache.commons:commons-csv:1.10.0"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0"
    implementation "org.flywaydb:flyway-core"
    implementation "org.jetbrains.exposed:exposed-spring-boot-starter:0.49.0"
    implementation "org.jetbrains.exposed:exposed-jdbc:0.49.0"
    implementation "org.jetbrains.exposed:exposed-json:0.49.0"
    implementation "org.jetbrains.exposed:exposed-kotlin-datetime:0.49.0"
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.5.0"
    implementation "org.hibernate.validator:hibernate-validator:8.0.1.Final"
    implementation "org.keycloak:keycloak-admin-client:23.0.7"

    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-security"

    implementation 'org.apache.commons:commons-compress:1.26.1'
    implementation 'com.github.luben:zstd-jni:1.5.6-2'
    implementation 'org.tukaani:xz:1.9'

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.mockito"
    }
    testImplementation "io.jsonwebtoken:jjwt-api:0.12.5"
    testImplementation "io.jsonwebtoken:jjwt-impl:0.12.5"
    testImplementation "io.jsonwebtoken:jjwt-jackson:0.12.5"
    testImplementation "com.ninja-squad:springmockk:4.0.2"
    testImplementation "org.testcontainers:postgresql:1.19.7"
    testImplementation "org.junit.platform:junit-platform-launcher:1.10.2"
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events TestLogEvent.FAILED
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
    }
}

tasks.named('bootBuildImage') {
    environment["BP_JVM_VERSION"] = "21"
    // https://paketo.io/docs/howto/configuration/
    environment["BPE_SPRING_PROFILES_ACTIVE"] = "docker"
}

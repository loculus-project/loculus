import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '1.9.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.21'
    id 'org.jlleitschuh.gradle.ktlint' version '11.6.1'
    id 'org.springdoc.openapi-gradle-plugin' version '1.8.0'
}

group = 'org.pathoplexus'
version = '0.0.1'

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

kotlin {
    jvmToolchain(21)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "io.github.microutils:kotlin-logging-jvm:3.0.5"
    implementation "org.postgresql:postgresql:42.7.0"
    implementation "org.apache.commons:commons-csv:1.10.0"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0"
    implementation "org.flywaydb:flyway-core"
    implementation "org.jetbrains.exposed:exposed-spring-boot-starter:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-json:$exposedVersion"
    implementation "org.jetbrains.exposed:exposed-kotlin-datetime:$exposedVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.4.1"
    implementation "org.hibernate.validator:hibernate-validator:8.0.1.Final"
    
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-security"
    
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.mockito"
    }
    testImplementation("io.jsonwebtoken:jjwt-api:0.12.3")
    testImplementation("io.jsonwebtoken:jjwt-impl:0.12.3")
    testImplementation("io.jsonwebtoken:jjwt-jackson:0.12.3")
    testImplementation "com.ninja-squad:springmockk:4.0.2"
    testImplementation "org.testcontainers:postgresql:1.19.3"
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events TestLogEvent.FAILED
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
    }
}

tasks.named('bootBuildImage') {
    environment["BP_JVM_VERSION"] = "21"
    // https://paketo.io/docs/howto/configuration/
    environment["BPE_SPRING_PROFILES_ACTIVE"] = "docker"
}

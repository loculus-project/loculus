import yaml
from pathlib import Path

with open("config/config.yaml") as f:
    config = yaml.safe_load(f)

with open("config/defaults.yaml") as f:
    defaults = yaml.safe_load(f)

# Merge configs, using defaults only as fallback
# Write to results/config.yaml
for key, value in defaults.items():
    if not key in config:
        config[key] = value

LOG_LEVEL = config.get("log_level", "INFO")
TIME_BETWEEN_ITERATIONS = config.get("time_between_iterations", 10)
MIN_BETWEEN_GITHUB_REQUESTS = config.get("min_between_github_requests", 2)
MIN_BETWEEN_ENA_CHECKS = config.get("min_between_ena_checks", 5)

SUBMIT_TO_ENA_PROD = config.get("submit_to_ena_prod", False)
if config.get("backend_url", "") not in config.get("allowed_submission_hosts", []):
    print("WARNING: backend_url not in allowed_hosts")
    SUBMIT_TO_ENA_PROD = False
SUBMIT_TO_ENA_DEV = not SUBMIT_TO_ENA_PROD

if SUBMIT_TO_ENA_DEV:
    print("Submitting to ENA dev environment")
    config["ena_submission_url"] = "https://wwwdev.ebi.ac.uk/ena/submit/drop-box/submit"
    config["github_url"] = (
        "https://raw.githubusercontent.com/pathoplexus/ena-submission/loculus_test/test/approved_ena_submission_list.json"
    )
    config["ena_reports_service_url"] = "https://wwwdev.ebi.ac.uk/ena/submit/report"

if SUBMIT_TO_ENA_PROD:
    print("WARNING: Submitting to ENA production")
    config["ena_submission_url"] = "https://www.ebi.ac.uk/ena/submit/drop-box/submit"
    config["github_url"] = (
        "https://raw.githubusercontent.com/pathoplexus/ena-submission/main/approved/approved_ena_submission_list.json"
    )
    config["ena_reports_service_url"] = "https://www.ebi.ac.uk/ena/submit/report"


Path("results").mkdir(parents=True, exist_ok=True)
with open("results/config.yaml", "w") as f:
    f.write(yaml.dump(config))


rule all:
    input:
        triggered="results/triggered",
        project_created="results/project_created",
        sample_created="results/sample_created",
        assembly_created="results/assembly_created",
        uploaded_external_metadata="results/uploaded_external_metadata",


rule get_ena_submission_list:
    input:
        script="scripts/get_ena_submission_list.py",
        config="results/config.yaml",
    output:
        released="results/ena_submission_list.json",
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        python {input.script} \
                --config-file {input.config} \
                --output-file {output.released} \
                --log-level {params.log_level} \
        """


rule trigger_submission_to_ena:
    input:
        script="scripts/trigger_submission_to_ena.py",
        config="results/config.yaml",
    output:
        submitted=touch("results/triggered"),
    params:
        log_level=LOG_LEVEL,
        min_between_github_requests=MIN_BETWEEN_GITHUB_REQUESTS,
    shell:
        """
        python {input.script} \
                --config-file {input.config} \
                --log-level {params.log_level} \
                --min-between-github-requests {params.min_between_github_requests}
        """


rule trigger_submission_to_ena_from_file:  # for testing
    input:
        script="scripts/trigger_submission_to_ena.py",
        input_file="results/approved_ena_submission_list.json",
        config="results/config.yaml",
    output:
        submitted=touch("results/triggered_from_file"),
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        python {input.script} \
                --config-file {input.config} \
                --input-file {input.input_file} \
                --log-level {params.log_level} \
        """


rule create_project:
    input:
        script="scripts/create_project.py",
        config="results/config.yaml",
    output:
        project_created=touch("results/project_created"),
    params:
        log_level=LOG_LEVEL,
        test_flag="--test" if SUBMIT_TO_ENA_DEV else "",
        time_between_iterations=TIME_BETWEEN_ITERATIONS,
    shell:
        """
        python {input.script} \
            --config-file {input.config} \
            --log-level {params.log_level} \
            --time-between-iterations {params.time_between_iterations} \
            {params.test_flag}
        """


rule create_sample:
    input:
        script="scripts/create_sample.py",
        config="results/config.yaml",
    output:
        sample_created=touch("results/sample_created"),
    params:
        log_level=LOG_LEVEL,
        test_flag="--test" if SUBMIT_TO_ENA_DEV else "",
        time_between_iterations=TIME_BETWEEN_ITERATIONS,
    shell:
        """
        python {input.script} \
            --config-file {input.config} \
            --log-level {params.log_level} \
            --time-between-iterations {params.time_between_iterations} \
            {params.test_flag}
        """


rule create_assembly:
    input:
        script="scripts/create_assembly.py",
        config="results/config.yaml",
    output:
        sample_created=touch("results/assembly_created"),
    params:
        log_level=LOG_LEVEL,
        test_flag="--test" if SUBMIT_TO_ENA_DEV else "",
        time_between_iterations=TIME_BETWEEN_ITERATIONS,
        min_between_ena_checks=MIN_BETWEEN_ENA_CHECKS,
    shell:
        """
        python {input.script} \
            --config-file {input.config} \
            --log-level {params.log_level} \
            --time-between-iterations {params.time_between_iterations} \
            --min-between-ena-checks {params.min_between_ena_checks} \
            {params.test_flag}
        """


rule upload_to_loculus:
    input:
        script="scripts/upload_external_metadata_to_loculus.py",
        config="results/config.yaml",
    output:
        sample_created=touch("results/uploaded_external_metadata"),
    params:
        log_level=LOG_LEVEL,
        time_between_iterations=TIME_BETWEEN_ITERATIONS,
    shell:
        """
        python {input.script} \
            --config-file {input.config} \
            --log-level {params.log_level} \
            --time-between-iterations {params.time_between_iterations} \
        """

import yaml
from pathlib import Path

with open("config/config.yaml") as f:
    config = yaml.safe_load(f)

with open("config/defaults.yaml") as f:
    defaults = yaml.safe_load(f)

# Merge configs, using defaults only as fallback
# Write to results/config.yaml
for key, value in defaults.items():
    if not key in config:
        config[key] = value

Path("results").mkdir(parents=True, exist_ok=True)
with open("results/config.yaml", "w") as f:
    f.write(yaml.dump(config))

LOG_LEVEL = config.get("log_level", "INFO")
ORGANISMS = config['organisms'].keys()


rule submit_all_external_metadata:
    input:
        expand("results/submitted_{organism}.json", organism=ORGANISMS)
    
rule submit_external_metadata:
    input:
        script="scripts/call_loculus.py",
        metadata="results/external_metadata_{organism}.ndjson",
        config="results/config.yaml",
    output:
        submitted="results/submitted_{organism}.json"
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        if [ -s {input.metadata} ]; then
            python {input.script} \
                --mode submit-external-metadata \
                --organism {wildcards.organism} \
                --metadata {input.metadata} \
                --config-file {input.config} \
                --output-file {output.submitted} \
                --log-level {params.log_level}
        fi
        """


rule get_ena_submission_list:
    input:
        script="scripts/get_ena_submission_list.py",
        config="results/config.yaml",
    output:
        released="results/ena_submission_list.json",
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        python {input.script} \
                --config-file {input.config} \
                --output-file {output.released} \
                --log-level {params.log_level} \
        """

rule trigger_submission_to_ena:
    input:
        script="scripts/trigger_submission_to_ena.py",
        config="results/config.yaml",
    output:
        submitted=touch("results/triggered"),
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        python {input.script} \
                --config-file {input.config} \
                --log-level {params.log_level} \
        """

rule trigger_submission_to_ena_from_file:  # for testing
    input:
        script="scripts/trigger_submission_to_ena.py",
        input_file="results/approved_ena_submission_list.json",
        config="results/config.yaml",
    output:
        submitted=touch("results/triggered_from_file"),
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        python {input.script} \
                --config-file {input.config} \
                --input-file {input.input_file} \
                --log-level {params.log_level} \
        """

rule create_project:
    input:
        script="scripts/create_project.py",
        config="results/config.yaml",
    output:
        project_created=touch("results/project_created"),
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        python {input.script} \
            --config-file {input.config} \
            --log-level {params.log_level} \
        """
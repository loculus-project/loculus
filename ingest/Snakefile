import yaml
from pathlib import Path

with open("config/defaults.yaml") as f:
    defaults = yaml.safe_load(f)

# Merge configs, using defaults only as fallback
# Write to results/config.yaml
for key, value in defaults.items():
    if not key in config:
        config[key] = value

# Infer whether nucleotide sequences are segmented
config["segmented"] = len(config["nucleotideSequences"]) > 1

Path("results").mkdir(parents=True, exist_ok=True)
with open("results/config.yaml", "w") as f:
    f.write(yaml.dump(config))

TAXON_ID = config["taxon_id"]
SEGMENTED = config["segmented"]
ALL_FIELDS = ",".join(config["all_fields"])
COLUMN_MAPPING = config["column_mapping"]
LOG_LEVEL = config.get("log_level", "INFO")


def rename_columns(input_file, output_file):
    with open(input_file, "r") as f:
        header = f.readline().strip().split("\t")
        header = [COLUMN_MAPPING.get(h, h) for h in header]
        with open(output_file, "w") as g:
            g.write("\t".join(header) + "\n")
            for line in f:
                g.write(line)


rule all:
    input:
        "results/submitted",
        "results/revised",
        "results/approved",


rule fetch_ncbi_dataset_package:
    # TODO: #1844 Set API key through secret
    output:
        dataset_package="results/ncbi_dataset.zip",
    shell:
        """
        datasets download virus genome taxon {TAXON_ID} \
            --no-progressbar \
            --filename {output.dataset_package} \
            --api-key 15c4ff96de265753f878bb08d88ca64df909 \
        """


rule extract_ncbi_dataset_sequences:
    """
    For unsegmented sequences, we only keep the sequence ID in the header.
    For segmented sequences, we keep full header as it contains segment information.
    """
    input:
        dataset_package="results/ncbi_dataset.zip",
    output:
        ncbi_dataset_sequences=(
            "results/sequences_full.fasta" if SEGMENTED else "results/sequences.fasta"
        ),
    params:
        header_arg="" if SEGMENTED else "-i",  # Keep full header if segmented
    shell:
        """
        unzip -jp {input.dataset_package} \
        ncbi_dataset/data/genomic.fna \
        | seqkit seq -w0 {params.header_arg} \
        > {output.ncbi_dataset_sequences}
        """


rule get_segment_details:
    """Check if viruses are segmented, if so add segment to metadata"""
    input:
        sequences="results/sequences_full.fasta",
        script="scripts/get_segment_details.py",
        ncbi_dataset_tsv="results/metadata_post_rename.tsv",
        config="results/config.yaml",
    output:
        sequences_processed="results/sequences.fasta",
        ncbi_dataset_tsv="results/metadata_post_segment.tsv",
    shell:
        """
        python {input.script} \
            --config-file {input.config} \
            --input-seq {input.sequences} \
            --input-metadata {input.ncbi_dataset_tsv} \
            --output-seq {output.sequences_processed} \
            --output-metadata {output.ncbi_dataset_tsv}
        """


rule calculate_sequence_hashes:
    """Output JSON: {insdc_accession: md5_sequence_hash, ...}"""
    input:
        sequences="results/sequences.fasta",
        script="scripts/calculate_sequence_hashes.py",
    output:
        sequence_hashes="results/sequence_hashes.json",
        sequence_json="results/sequences.json",
    shell:
        """
        python scripts/calculate_sequence_hashes.py \
            --input {input.sequences} \
            --output-hashes {output.sequence_hashes} \
            --output-sequences {output.sequence_json}
        """


rule format_ncbi_dataset_report:
    input:
        dataset_package="results/ncbi_dataset.zip",
    output:
        ncbi_dataset_tsv="results/metadata_post_extract.tsv",
    params:
        fields_to_include=ALL_FIELDS,
    shell:
        """
        dataformat tsv virus-genome \
            --package {input.dataset_package} \
            --fields {params.fields_to_include:q} \
            > {output.ncbi_dataset_tsv}
        """


rule rename_columns:
    input:
        ncbi_dataset_tsv="results/metadata_post_extract.tsv",
    output:
        ncbi_dataset_tsv="results/metadata_post_rename.tsv",
    run:
        rename_columns(input.ncbi_dataset_tsv, output.ncbi_dataset_tsv)


rule prepare_metadata:
    input:
        metadata=(
            "results/metadata_post_segment.tsv"
            if SEGMENTED
            else "results/metadata_post_rename.tsv"
        ),
        sequence_hashes="results/sequence_hashes.json",
        config="results/config.yaml",
        script="scripts/prepare_metadata.py",
    output:
        metadata="results/metadata_post_prepare.json",
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        python scripts/prepare_metadata.py \
            --config-file {input.config} \
            --input {input.metadata} \
            --sequence-hashes {input.sequence_hashes} \
            --output {output.metadata} \
            --log-level {params.log_level} \
        """


rule group_segments:
    input:
        metadata="results/metadata_post_prepare.json",
        sequences="results/sequences.json",
        config="results/config.yaml",
        script="scripts/group_segments.py",
    output:
        metadata="results/metadata_post_group.json",
        sequences="results/sequences_post_group.json",
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        python scripts/group_segments.py \
            --config-file {input.config} \
            --input-metadata {input.metadata} \
            --input-seq {input.sequences} \
            --output-metadata {output.metadata} \
            --output-seq {output.sequences} \
            --log-level {params.log_level} \
        """


def get_grouped_metadata(wildcard):
    if wildcard:
        return ("results/metadata_post_group.json",)
    else:
        return ("results/metadata_post_prepare.json",)


rule get_previous_submissions:
    """Download metadata and sequence hashes of all previously submitted sequences 
    Produces mapping from INSDC accession to loculus id/version/hash
    insdc_accession:
        loculus_accession: abcd
        versions:
        -   version: 1
            hash: abcd
            status: APPROVED_FOR_RELEASE
        -   version: 2
            hash: efg
            status: HAS_ERRORS
        ...
    """
    input:
        # Reduce likelihood of race condition of multi-submission
        # By delaying the start of the script
        prepped_metadata=(
            "results/metadata_post_group.json"
            if SEGMENTED
            else "results/metadata_post_prepare.json"
        ),
        config="results/config.yaml",
        script="scripts/call_loculus.py",
    output:
        hashes="results/previous_submissions.json",
    params:
        log_level=LOG_LEVEL,
        sleep=config["post_start_sleep"],
    shell:
        """
        sleep {params.sleep}
        python scripts/call_loculus.py \
            --mode get-submitted \
            --config-file {input.config} \
            --log-level {params.log_level} \
            --output {output.hashes}
        """


rule compare_hashes:
    input:
        config="results/config.yaml",
        old_hashes="results/previous_submissions.json",
        metadata=(
            "results/metadata_post_group.json"
            if SEGMENTED
            else "results/metadata_post_prepare.json"
        ),
        script="scripts/compare_hashes.py",
    output:
        to_submit="results/to_submit.json",
        to_revise="results/to_revise.json",
        unchanged="results/unchanged.json",
        blocked="results/blocked.json",
    params:
        log_level=LOG_LEVEL,
        subsample_fraction=config.get("subsample_fraction", 1.0),
    shell:
        """
        python scripts/compare_hashes.py \
            --config-file {input.config} \
            --old-hashes {input.old_hashes} \
            --metadata {input.metadata} \
            --to-submit {output.to_submit} \
            --to-revise {output.to_revise} \
            --unchanged {output.unchanged} \
            --output-blocked {output.blocked} \
            --log-level {params.log_level} \
            --subsample-fraction {params.subsample_fraction} \
        """


rule prepare_files:
    input:
        config="results/config.yaml",
        metadata=(
            "results/metadata_post_group.json"
            if SEGMENTED
            else "results/metadata_post_prepare.json"
        ),
        sequences=(
            "results/sequences_post_group.json"
            if SEGMENTED
            else "results/sequences.json"
        ),
        to_submit="results/to_submit.json",
        to_revise="results/to_revise.json",
        script="scripts/prepare_files.py",
    output:
        sequences_submit="results/submit_sequences.fasta",
        sequences_revise="results/revise_sequences.fasta",
        metadata_submit="results/submit_metadata.tsv",
        metadata_revise="results/revise_metadata.tsv",
    shell:
        """
        python scripts/prepare_files.py \
            --config-file {input.config} \
            --metadata-path {input.metadata} \
            --sequences-path {input.sequences} \
            --to-submit-path {input.to_submit} \
            --to-revise-path {input.to_revise} \
            --metadata-submit-path {output.metadata_submit} \
            --metadata-revise-path {output.metadata_revise} \
            --sequences-submit-path {output.sequences_submit} \
            --sequences-revise-path {output.sequences_revise} \
        """


rule submit:
    input:
        metadata="results/submit_metadata.tsv",
        sequences="results/submit_sequences.fasta",
        config="results/config.yaml",
        script="scripts/call_loculus.py",
    output:
        submitted=touch("results/submitted"),
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        if [ -s {input.metadata} ]; then
            python scripts/call_loculus.py \
                --mode submit \
                --metadata {input.metadata} \
                --sequences {input.sequences} \
                --config-file {input.config} \
                --log-level {params.log_level}
        fi
        """


rule revise:
    input:
        metadata="results/revise_metadata.tsv",
        sequences="results/revise_sequences.fasta",
        config="results/config.yaml",
        script="scripts/call_loculus.py",
    output:
        revised=touch("results/revised"),
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        if [ -s {input.metadata} ]; then
            python scripts/call_loculus.py \
                --mode revise \
                --metadata {input.metadata} \
                --sequences {input.sequences} \
                --config-file {input.config} \
                --log-level {params.log_level}
        fi
        """


rule approve:
    input:
        config="results/config.yaml",
        script="scripts/call_loculus.py",
    output:
        approved=touch("results/approved"),
    params:
        log_level=LOG_LEVEL,
    shell:
        """
        python scripts/call_loculus.py \
            --mode approve \
            --config-file {input.config} \
            --log-level {params.log_level} \
        """

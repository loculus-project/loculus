TAXON_ID = 10244  # Mpox
BRANCH = "cfgbl-prepro"
NUMBER_OF_SAMPLES = 500


rule all:
    input:
        "data/sequences.fasta",
        "data/metadata.tsv",


rule fetch_ncbi_dataset_package:
    output:
        dataset_package="data/ncbi_dataset.zip",
    retries: 5
    shell:
        """
        datasets download virus genome taxon {TAXON_ID} \
            --no-progressbar \
            --filename {output.dataset_package}
        """


rule extract_ncbi_dataset_sequences:
    input:
        dataset_package="data/ncbi_dataset.zip",
    output:
        ncbi_dataset_sequences="data/sequences.fasta",
    shell:
        """
        unzip -jp {input.dataset_package} \
            ncbi_dataset/data/genomic.fna \
        | seqkit seq -i -w0 \
        > {output.ncbi_dataset_sequences}
        """


rule format_ncbi_dataset_report:
    input:
        dataset_package="data/ncbi_dataset.zip",
    output:
        ncbi_dataset_tsv="data/metadata_raw.tsv",
    params:
        fields_to_include="accession,isolate-collection-date,geo-location,geo-region,isolate-lineage,release-date,submitter-affiliation,submitter-names",
    shell:
        """
        dataformat tsv virus-genome \
            --package {input.dataset_package} \
            --fields {params.fields_to_include:q} \
            > {output.ncbi_dataset_tsv}
        """


rule rename_columns:
    input:
        ncbi_dataset_tsv="data/metadata_raw.tsv",
    output:
        ncbi_dataset_tsv="data/metadata.tsv",
    shell:
        """
        sed '1s/Accession/submissionId/;\
                1s/Isolate Collection date/collection_date/; \
                1s/Release date/ncbi_release_date/; \
                1s/Geographic Location/country/; \
                1s/Submitter Affiliation/author_affiliation/; \
                1s/Submitter Names/authors/; \
                1s/Isolate Lineage/isolate_name/;' \
            {input.ncbi_dataset_tsv} \
            > {output.ncbi_dataset_tsv}
        """


rule subsample:
    input:
        ncbi_dataset_tsv="data/metadata.tsv",
        ncbi_dataset_sequences="data/sequences.fasta",
    output:
        ncbi_dataset_tsv="data/metadata_sample.tsv",
        ncbi_dataset_sequences="data/sequences_sample.fasta",
        strains="data/strains_sample.txt",
    params:
        number_of_samples=NUMBER_OF_SAMPLES,
    shell:
        """
        tsv-sample -H -n {params.number_of_samples} \
            <{input.ncbi_dataset_tsv} \
            >{output.ncbi_dataset_tsv}
        tsv-select -H -f submissionId \
            <{output.ncbi_dataset_tsv} \
            >{output.strains}
        seqkit grep -n -f {output.strains} \
            {input.ncbi_dataset_sequences} \
            >{output.ncbi_dataset_sequences}
        """


rule submit_to_loculus:
    input:
        metadata="data/metadata_sample.tsv",
        sequences="data/sequences_sample.fasta",
        submit_config="config/submit_config.yaml",
    output:
        ids="result/loculus_ids.tsv",
    params:
        branch=BRANCH,
    shell:
        """
        python scripts/submit_to_loculus.py \
            --mode submit \
            --metadata {input.metadata} \
            --branch {params.branch} \
            --sequences {input.sequences} \
            --output-ids {output.ids}
        """


rule approve:
    params:
        branch=BRANCH,
    shell:
        """
        python scripts/submit_to_loculus.py \
            --mode approve
            --branch {params.branch}
        """
